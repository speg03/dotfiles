# -*- mode: sh; sh-shell: zsh -*-

# ------------------------------
# Environment Variables
# ------------------------------

export GPG_TTY="$(tty)"
export LESS="-giMR"

export HISTFILE="$HOME/.cache/zsh/history"
export HISTSIZE=2000
export SAVEHIST=1000

# ------------------------------
# Aliases
# ------------------------------

alias e='emacs'
alias ll='ls -al'
alias magit='emacs --funcall magit-status'
alias t='tmux-new-or-switch-session'

# ------------------------------
# Key bindings
# ------------------------------

fuzzy-cd-repo() {
    local target_dir=""
    local repo_root=$(git rev-parse --show-toplevel 2>/dev/null)
    if [[ -n $repo_root ]]; then
        local selected_path=$({ echo "."; git ls-tree -dr --name-only --full-tree HEAD; } | fzf -q "$LBUFFER") || return 0
        if [[ -n $selected_path ]]; then
            target_dir="$repo_root/$selected_path"
        fi
    else
        target_dir=$(ghq list -p | fzf -q "$LBUFFER") || return 0
    fi

    if [[ -n $target_dir ]]; then
        BUFFER="cd $target_dir"
        zle accept-line
    fi
    zle -R -c
}
zle -N fuzzy-cd-repo
bindkey '^@' fuzzy-cd-repo

# ------------------------------
# Completions
# ------------------------------

autoload bashcompinit && bashcompinit
autoload -Uz compinit

zstyle ':completion:*:default' menu select=1
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'

compinit -i -u -d "$HOME/.cache/zsh/compdump"

# ------------------------------
# OS specific
# ------------------------------

if [[ $(uname -s) == Darwin ]]; then
    alias ls='ls -G'
    source-if-exists "/opt/homebrew/opt/fzf/shell/key-bindings.zsh"
    source-if-exists "/opt/homebrew/opt/fzf/shell/completion.zsh"
    source-if-exists "/opt/homebrew/share/google-cloud-sdk/completion.zsh.inc"
elif [[ $(uname -s) == Linux ]]; then
    alias ls='ls --color=auto'
    source-if-exists "/usr/share/fzf/shell/key-bindings.zsh"
    source-if-exists "/usr/share/doc/fzf/examples/key-bindings.zsh"
    source-if-exists "/usr/share/google-cloud-sdk/completion.zsh.inc"
    source-if-exists "/snap/google-cloud-cli/current/completion.zsh.inc"
    if [[ -f /snap/aws-cli/current/aws/dist/aws_completer ]]; then
        complete -C '/snap/aws-cli/current/aws/dist/aws_completer' aws
    fi
fi

# ------------------------------
# Startup
# ------------------------------

if exists starship; then
    eval "$(starship init zsh)"
fi

if exists tmux && [[ $TERM != screen* && $TERM_PROGRAM != vscode ]]; then
    tmux-new-or-switch-session
fi

source-if-exists "$HOME/.config/zsh/zshrc.local"
